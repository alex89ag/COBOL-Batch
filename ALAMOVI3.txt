000100CBL LIB
000200 IDENTIFICATION DIVISION.
000300*************************************************************
000400*      REPORTE DE MOVIMIENTOS EN CUENTAS DE CLIENTES        *
000500*************************************************************
000600 PROGRAM-ID.    ALAMOVI3.
000700 ENVIRONMENT DIVISION.
000800 CONFIGURATION SECTION.
000900 SPECIAL-NAMES.
001000      DECIMAL-POINT IS COMMA.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300*    ENTRADA ARCHIVO ESDS
001400     SELECT ENTRADA ASSIGN TO AS-MOVI
001500          ORGANIZATION IS SEQUENTIAL
001600          ACCESS IS SEQUENTIAL
001700          FILE STATUS IS FS-MOV.
001800*    CLIENTES KSDS ACCEDIDO PARA MODIFICACIONES
001900     SELECT CLIENTES  ASSIGN TO MAECLI
002000          ORGANIZATION IS INDEXED
002100          ACCESS IS RANDOM
002200          RECORD KEY IS CLI-CLAVE
002300          FILE STATUS IS FS-CLI.
002400*    SALIDA A LA IMPRESORA
002500     SELECT REPORTE ASSIGN TO REPORTE.
002501*    GUARDA EL ULTIMO NUMERO DE CLIENTE USADO
002510     SELECT PARAMETRO ASSIGN TO PARAM
002520          ORGANIZATION IS INDEXED
002530          ACCESS IS RANDOM
002540          RECORD KEY  IS PRM-CLAVE
002550          FILE STATUS IS FS-PRM.
002560
002700 DATA DIVISION.
002800 FILE SECTION.
002900 FD  ENTRADA.
002910     COPY  INPMOV.
003700
003800 FD  CLIENTES.
003900     COPY  MAECLIE.
004000
004410 FD  PARAMETRO.
004420 01  PRM-REG.
004430     05 PRM-CLAVE.
004440        10 PRM-CLAVE1 PIC 99.
004450        10 PRM-CLAVE2 PIC X(18).
004460     05 PRM-CNT.
004470        10 PRM-N-NRO  PIC 9(8).
004480     05 FILLER        PIC X(72).
004490
004500 FD  REPORTE
004510     RECORDING MODE F.
004520 01  LINEA.
004530     05 FILLER  PIC X(132).
004540
004600 WORKING-STORAGE SECTION.
004700 01  WS-ESTADOS.
004800     05  FS-CLI             PIC XX.
004900     05  FS-MOV             PIC XX.
004901 01  WS-IO-PRM              PIC X(4).
004910 01  FS-PRM                 PIC XX.
005000 01  WS-FIN                 PIC 9        VALUE 0.
005100     88 NOEOF                            VALUE 0.
005200     88 EOFILE                           VALUE 1.
005300 01  WS-CANT-LIN            PIC 9(2)     VALUE 21.
005400 01  WS-CONTROL             PIC 9        VALUE 2.
005500 01  WS-MONTO               PIC S9(9)V99 VALUE +0.
005600 01  WS-NUM                 PIC 9(11)    VALUE 0.
005700 01  WS-NUM-PAG             PIC 999      VALUE 0.
005800 01  WS-SALDO-ANTERIOR      PIC S9(9)V99 COMP-3.
005810 01  WS-ESPACIO             PIC X(132)   VALUE SPACES.
005900 01  WS-APE-LONG            PIC 99.
006000 01  WS-TIPO-REPO           PIC 9        VALUE 0.
006001 01  WS-VALIDO              PIC 9        VALUE 1.
006010 01  WS-APE-NOM             PIC X(30).
006100
006200 01  FECHA-ACTUAL.
006300     05                     PIC X(90)    VALUE SPACES.
006400     05                     PIC X(7)     VALUE 'FECHA:'.
006500     05 FECHA               PIC 99/99/99.
006600     05                     PIC X(6)     VALUE SPACES.
006700     05                     PIC X(6)     VALUE 'HORA:'.
006800     05 HORA                PIC 99.99.99B99.
006900     05                     PIC X(4)     VALUE ' Hs.'.
007000
007100 01  TITULO.
007200     05               PIC X(35) VALUE SPACES.
007300     05               PIC X(22) VALUE 'Movimientos de cuentas'.
007400     05               PIC X(86) VALUE SPACES.
007500
007600 01  PAGINA.
007700     05               PIC X(1)  VALUE SPACES.
007800     05               PIC X(17) VALUE 'Numero de pagina:'.
007900     05 PRT-NUM-PAG   PIC Z99.
008000     05               PIC X(86) VALUE SPACES.
008100
008200 01  MOV-COLS-SUP.
008300     05               PIC X(1)  VALUE SPACES.
008400     05               PIC X(12) VALUE '   Cuenta'.
008500     05               PIC X(2)  VALUE SPACES.
008600     05               PIC X(30) VALUE '          Cliente'.
008700     05               PIC X(2)  VALUE SPACES.
008800     05               PIC X(10) VALUE 'Movimiento'.
008900     05               PIC X(2)  VALUE SPACES.
009000     05               PIC X(49)
009100                      VALUE '                 Detalle'.
009200     05               PIC X(15)  VALUE SPACES.
009500
009600 01  MOV-COLS-INF.
009700     05               PIC X(1)  VALUE SPACES.
009800     05               PIC X(4)  VALUE 'Tipo'.
010000     05               PIC X(8)  VALUE ' Numero'.
010100     05               PIC X(2)  VALUE SPACES.
010200     05               PIC X(30)
010300                      VALUE 'Apellido/Nombre o Razon Social'.
010500     05               PIC X(14) VALUE SPACES.
010700     05               PIC X(15) VALUE 'Saldo anterior'.
010800     05               PIC X(2)  VALUE SPACES.
010900     05               PIC X(15) VALUE '         Monto'.
011000     05               PIC X(2)  VALUE SPACES.
011100     05               PIC X(15) VALUE '  Saldo actual'.
011410
011600 01  PRINT-DET-MOV.
011700     05               PIC X(1)  VALUE SPACES.
011800     05 PDM-TIP       PIC 9.
011900     05               PIC X(1)  VALUE SPACES.
012000     05 PDM-NRO       PIC zzzzzzz9/9.
012100     05               PIC X(2)  VALUE SPACES.
012200     05 PDM-APE-NOM   PIC X(30).
012300     05               PIC X(2)  VALUE SPACES.
012400     05 PDM-MOVI      PIC X(10).
012500     05               PIC X(2)  VALUE SPACES.
012600     05 PDM-SAL-ANT   PIC $$$.$$$.$$9,99-.
012700     05               PIC X(2)  VALUE SPACES.
012800     05 PDM-IMPORTE   PIC $$$.$$$.$$9,99-.
012900     05               PIC X(2)  VALUE SPACES.
013000     05 PDM-SAL-ACT   PIC $$$.$$$.$$9,99-.
013010     05               PIC X(15) VALUE SPACES.
013400
013401 01  CLI-COLS-SUP.
013402     05               PIC X(1)  VALUE SPACES.
013403     05               PIC X(12) VALUE '  Cuenta'.
013404     05               PIC X(2)  VALUE SPACES.
013405     05               PIC X(30) VALUE '    Cliente'.
013406     05               PIC X(2)  VALUE SPACES.
013407     05               PIC X(10) VALUE 'Movimiento'.
013409     05               PIC X(2)  VALUE SPACES.
013410     05               PIC X(13) VALUE '   DNI'.
013411     05               PIC X(2)  VALUE SPACES.
013412     05               PIC X(10) VALUE 'Nacimiento'.
013413     05               PIC X(2)  VALUE SPACES.
013414     05               PIC X(27) VALUE 'Direccion'.
013416     05               PIC X(2)  VALUE SPACES.
013417     05               PIC X(6)  VALUE 'C. P.'.
013418     05               PIC X(2)  VALUE SPACES.
013422
013423 01  CLI-COLS-INF.
013424     05               PIC X(1)  VALUE SPACES.
013425     05               PIC X(4)  VALUE 'Tipo'.
013426     05               PIC X(8)  VALUE ' Numero'.
013427     05               PIC X(2)  VALUE SPACES.
013428     05               PIC X(30)
013429                      VALUE 'Apellido/Nombre o Razon Social'.
013430     05               PIC X(14) VALUE SPACES.
013431     05               PIC X(4)  VALUE 'Tipo'.
013432     05               PIC X(1)  VALUE SPACES.
013433     05               PIC X(8)  VALUE 'Numero'.
013434     05               PIC X(14) VALUE SPACES.
013435     05               PIC X(20) VALUE 'Calle'.
013436     05               PIC X(1)  VALUE SPACES.
013437     05               PIC X(6)  VALUE 'Nro'.
013438     05               PIC X(8)  VALUE SPACES.
013439
013440 01  PRINT-DET-CLI.
013441     05               PIC X(1)  VALUE SPACES.
013442     05 PDC-TIP       PIC X.
013443     05               PIC X(1)  VALUE SPACES.
013444     05 PDC-NRO       PIC zzzzzzz9/9.
013445     05               PIC X(2)  VALUE SPACES.
013446     05 PDC-APE-NOM   PIC X(30).
013447     05               PIC X(2)  VALUE SPACES.
013448     05 PDC-MOVI      PIC X(10).
013449     05               PIC X(2)  VALUE SPACES.
013450     05 PDC-TIPDOC    PIC X(4).
013451     05               PIC X(1)  VALUE SPACES.
013452     05 PDC-NRODOC    PIC X(8).
013453     05               PIC X(2)  VALUE SPACES.
013454     05 PDC-FECNAC.
013455        10 PDC-FECNAC-D PIC 99.
013456        10 PDC-FECNAC-M PIC /99/.
013457        10 PDC-FECNAC-A PIC 9999.
013458     05               PIC X(2)  VALUE SPACES.
013459     05 PDC-CALLE     PIC X(20).
013460     05               PIC X(1)  VALUE SPACES.
013461     05 PDC-CALLE-NRO PIC X(6).
013462     05               PIC X(2)  VALUE SPACES.
013463     05 PDC-CODPOST   PIC X(6).
013464     05               PIC X(2)  VALUE SPACES.
013507
013508 01  PRINT-DET-INV.
013509     05               PIC X     VALUE SPACES.
013510     05 PDI-TIP       PIC X.
013511     05               PIC X     VALUE SPACES.
013512     05 PDI-NRO       PIC zzzzzzz9/9.
013513     05               PIC X(2)  VALUE SPACES.
013514     05 PDI-APE-NOM   PIC X(30).
013515     05               PIC X(2)  VALUE SPACES.
013516     05 PDI-MOVI      PIC X(10).
013517     05               PIC X(2)  VALUE SPACES.
013518     05               PIC X(32)
013519                      VALUE '****INGRESO INVALIDO! ERROR EN:'.
013520     05 DETALLE-INV   PIC X(34).
013521     05               PIC X(4)  VALUE '****'.
013522
013530 PROCEDURE DIVISION.
013600 0000-INICIO.
013700     PERFORM ABRIR-LEER-ARCHIVOS.
013800
013900     ACCEPT FECHA     FROM DATE.
014000     ACCEPT HORA      FROM TIME.
014100
014200     PERFORM IMPRIMIR-TIT-FECHA.
014300
014400     PERFORM UNTIL EOFILE
014500        IF WS-CANT-LIN > 20 THEN PERFORM IMPRIMIR-PAGINA
014600        ELSE ADD 1 TO WS-CANT-LIN
014700        END-IF
014800        EVALUATE INP-TIPO-MOV
014900            WHEN 'C' THRU 'D'
015000                PERFORM CREDITO-DEBITO
015300            WHEN 'A'
015400                PERFORM ALTA
015500            WHEN 'B'
015600                PERFORM BAJA
015700            WHEN 'M'
015800                PERFORM MODIFICAR
015900            WHEN OTHER
016000                DISPLAY 'MOVIMIENTO INVALIDO'
016100        END-EVALUATE
016200
016300        READ ENTRADA AT END SET EOFILE TO TRUE
016400        END-READ
016500
016600     END-PERFORM.
016700
016800     CLOSE ENTRADA CLIENTES.
016900     GOBACK.
017000
017100 ABRIR-LEER-ARCHIVOS.
017200     OPEN INPUT ENTRADA I-O CLIENTES OUTPUT REPORTE.
017300     IF WS-ESTADOS NOT = '0000' THEN
017400              DISPLAY 'ERROR AL ABRIR' WS-ESTADOS
017500              MOVE 12 TO RETURN-CODE
017600              GOBACK
017700     END-IF
017800
017900     READ ENTRADA AT END  DISPLAY 'ARCHIVO VACIO' GOBACK.
018000     IF FS-MOV NOT = '00' THEN
018100              DISPLAY 'ERROR AL LEER' WS-ESTADOS
018200              MOVE 12 TO RETURN-CODE
018300              GOBACK
018400     END-IF.
018500
018600 CREDITO-DEBITO.
018700        IF INP-MONTO IS NUMERIC
018800             MOVE INP-MONTO TO WS-NUM
018900             MOVE INP-CLAVE TO CLI-CLAVE
019000             READ CLIENTES
019100             IF FS-CLI NOT = '00' THEN
019200                 DISPLAY 'RECORD NOT FOUND!' INP-MOV
019300                 MOVE 'NO SE ENCONTRO LA CUENTA' TO DETALLE-INV
019310                 PERFORM IMPRIMIR-NO-VALIDO
019400             ELSE
019500                 MOVE CLI-SALDO TO WS-SALDO-ANTERIOR
019600                 COMPUTE WS-MONTO = WS-NUM / 100
019800                 IF INP-TIPO-MOV = 'C'
019900                     COMPUTE CLI-SALDO = CLI-SALDO + WS-MONTO
020000                     MOVE 'CREDITO'    TO PDM-MOVI
020100                 ELSE
020200                     COMPUTE CLI-SALDO = CLI-SALDO + WS-MONTO
020300                     MOVE 'DEBITO'     TO PDM-MOVI
020400                 END-IF
020500                 PERFORM REGRABAR
020600                 PERFORM IMPRIMIR-REPO-MOV
020610             END-IF
020700        ELSE
020710             MOVE 'SALDO INVALIDO' TO DETALLE-INV
020800             PERFORM IMPRIMIR-NO-VALIDO
020900        END-IF.
021000
021100 ALTA.
021101
021105     MOVE '1'             TO WS-VALIDO
021106     PERFORM NUEVO-NUM
021133     MOVE 'ALTA'    TO PDC-MOVI
021135     MOVE INP-A-TIP-CLI   TO CLI-TIP
021136     MOVE PRM-N-NRO       TO CLI-NRO-NRO
021137     MOVE '1'             TO CLI-DIG-VER
021140     MOVE INP-A-NOMBRE    TO CLI-NOMBRE
021142     MOVE INP-A-APELLIDO  TO CLI-APELLIDO
021144     MOVE INP-A-TIPDOC    TO CLI-TIP-DOC
021145*    IF INP-A-NRODOC IS NUMERIC THEN
021147       MOVE INP-A-NRODOC  TO CLI-NRO-DOC
021148*    ELSE
021149*      MOVE ZERO          TO WS-VALIDO
021150*    END-IF
021152*    IF INP-A-FECNAC IS NUMERIC THEN
021153       MOVE INP-A-FECNAC  TO CLI-FEC-NAC
021154*    ELSE
021155*      MOVE ZERO          TO WS-VALIDO
021156*    END-IF
021158     MOVE INP-A-PROV      TO CLI-PROVINCIA
021160     MOVE INP-A-LOCAL     TO CLI-LOCALIDAD
021162     MOVE INP-A-CALLE     TO CLI-CALLE
021165*    IF INP-M-CALLE-NRO IS NUMERIC THEN
021166       MOVE INP-A-CALLE-NRO TO CLI-LOC-NUMERO
021167*    ELSE
021168*      MOVE ZERO          TO WS-VALIDO
021169*    END-IF
021171     MOVE INP-M-CODPOST   TO CLI-ZIP
021172     MOVE 0               TO  CLI-SALDO
021173     MOVE 0               TO  CLI-FECACT
021210     IF WS-VALIDO = 1 THEN
021320       PERFORM GRABAR
021330       PERFORM IMPRIMIR-REPO-CLI
021340     ELSE
021350       PERFORM IMPRIMIR-NO-VALIDO
021360     END-IF.
021370
021400 BAJA.
021401     MOVE INP-CLAVE TO CLI-CLAVE
021402     READ CLIENTES
021403     IF FS-CLI NOT = '00' THEN
021404         DISPLAY 'RECORD NOT FOUND!' INP-MOV
021405         MOVE 'NO SE ENCONTRO LA CUENTA' TO DETALLE-INV
021408         PERFORM IMPRIMIR-NO-VALIDO
021409     ELSE
021410         MOVE 'BAJA'    TO PDC-MOVI
021510         PERFORM IMPRIMIR-REPO-CLI
021520         DELETE CLIENTES
021530         END-DELETE
021600     END-IF.
021610
021700 MODIFICAR.
021701     MOVE 1     TO WS-VALIDO
021702     STRING
021703         INP-M-TIP-CLI DELIMITED BY SIZE
021704         INP-M-NRO-CLI DELIMITED BY SIZE
021705         INTO CLI-CLAVE
021706     END-STRING
021708     READ CLIENTES
021709     IF FS-CLI NOT = '00' THEN
021710         DISPLAY 'RECORD NOT FOUND!' INP-MOV
021711         MOVE 'NO SE ENCONTRO LA CUENTA' TO DETALLE-INV
021714         PERFORM IMPRIMIR-NO-VALIDO
021715     ELSE
021716         MOVE 'MODIFICAR'    TO PDC-MOVI
021720         MOVE INP-M-TIP-CLI   TO CLI-TIP
021730         MOVE INP-M-NRO-CLI   TO CLI-NRO
021731         EVALUATE INP-M-NRO
021732           WHEN '01'
021733             MOVE INP-M-NOMBRE    TO CLI-NOMBRE
021734           WHEN '02'
021750             MOVE INP-M-APELLIDO  TO CLI-APELLIDO
021751           WHEN '03'
021760             MOVE INP-M-TIP-DOC   TO CLI-TIP-DOC
021761           WHEN '04'
021762             IF INP-M-NRODOC IS NUMERIC THEN
021770               MOVE INP-M-NRODOC  TO CLI-NRO-DOC
021772             ELSE
021773               MOVE ZERO          TO WS-VALIDO
021774             END-IF
021775           WHEN '05'
021781             IF INP-M-FECNAC IS NUMERIC THEN
021782               MOVE INP-M-FECNAC  TO CLI-FEC-NAC
021784             ELSE
021785               MOVE ZERO          TO WS-VALIDO
021786             END-IF
021787           WHEN '06'
021790             MOVE INP-M-PROV      TO CLI-PROVINCIA
021791           WHEN '07'
021800             MOVE INP-M-LOCAL     TO CLI-LOCALIDAD
021801           WHEN '08'
021802             MOVE INP-M-CALLE     TO CLI-CALLE
021803           WHEN '09'
021805             IF INP-M-CALLE-NRO IS NUMERIC THEN
021806               MOVE INP-M-CALLE-NRO TO CLI-LOC-NUMERO
021808             ELSE
021809               MOVE ZERO          TO WS-VALIDO
021810             END-IF
021811           WHEN '10'
021812             MOVE INP-M-CODPOST   TO CLI-ZIP
021813         END-EVALUATE
021814         IF WS-VALIDO = 1 THEN
021815           PERFORM REGRABAR
021816           PERFORM IMPRIMIR-REPO-CLI
021817         ELSE
021818           PERFORM IMPRIMIR-NO-VALIDO
021820         END-IF
021821     END-IF.
021822
022016 NUEVO-NUM.
022017
022018     OPEN I-O PARAMETRO.
022019     IF FS-PRM NOT = '00' THEN
022020              DISPLAY 'ERROR AL ABRIR PARAMETRO' FS-PRM
022021              MOVE 12 TO RETURN-CODE
022022              GOBACK
022023     END-IF
022024     MOVE 01 TO PRM-CLAVE1
022025     MOVE SPACES TO PRM-CLAVE2
022026     READ PARAMETRO
022027     IF FS-PRM NOT EQUAL '00'
022028        DISPLAY 'ERROR AL LEER PARAMETRO' FS-PRM
022029        GOBACK
022030     END-IF
022031     DISPLAY 'ULTIMO NRO: ' PRM-N-NRO
022032     COMPUTE PRM-N-NRO = PRM-N-NRO + 1
022033     DISPLAY 'NUEVO NRO: ' PRM-N-NRO
022034     REWRITE PRM-REG
022035     IF FS-PRM NOT EQUAL '00'
022036        DISPLAY 'ERROR AL ESCRIBIR ' FS-PRM
022037     END-IF
022038     CLOSE PARAMETRO.
022039
022040 REGRABAR.
022100     REWRITE CLI-REGISTRO
022200     IF FS-CLI NOT = '00' THEN
022300         DISPLAY 'ERROR AL ACTUALIZAR ' FS-CLI
022400     END-IF.
022500
022510 GRABAR.
022520     WRITE CLI-REGISTRO
022530     IF FS-CLI NOT = '00' THEN
022540         DISPLAY 'ERROR AL GRABAR ' FS-CLI
022550     END-IF.
022560
022600 IMPRIMIR-TIT-FECHA.
022700     WRITE LINEA FROM FECHA-ACTUAL
022800     WRITE LINEA FROM TITULO.
022900
023000 IMPRIMIR-PAGINA.
023100     ADD 1           TO WS-NUM-PAG
023200     MOVE ZERO       TO WS-CANT-LIN
023300     MOVE WS-NUM-PAG TO PRT-NUM-PAG
023400     MOVE 2          TO WS-CONTROL
023500     WRITE LINEA FROM PAGINA       AFTER WS-CONTROL
023600     PERFORM IMPRIMIR-NOM-COLS.
023700
023800 IMPRIMIR-NOM-COLS.
023801     IF WS-TIPO-REPO = '0' THEN
023803         IF INP-TIPO-MOV = 'C' OR INP-TIPO-MOV = 'D' THEN
023804             MOVE '1' TO WS-TIPO-REPO
023805         ELSE
023806             MOVE '2' TO WS-TIPO-REPO
023807         END-IF
023808     END-IF
023810     IF WS-TIPO-REPO = '1' THEN
023900         WRITE LINEA FROM MOV-COLS-SUP AFTER WS-CONTROL
024000         WRITE LINEA FROM MOV-COLS-INF AFTER WS-CONTROL
024001     ELSE
024010         WRITE LINEA FROM CLI-COLS-SUP AFTER WS-CONTROL
024020         WRITE LINEA FROM CLI-COLS-INF AFTER WS-CONTROL
024100     END-IF.
024110
024200 IMPRIMIR-REPO-CLI.
024210     IF WS-TIPO-REPO NOT EQUAL '2' THEN
024211        MOVE '2' TO WS-TIPO-REPO
024212        WRITE LINEA FROM WS-ESPACIO
024220        PERFORM IMPRIMIR-NOM-COLS
024221        ADD 3 TO WS-CANT-LIN
024222     END-IF.
024223     MOVE CLI-TIP           TO PDC-TIP
024225     MOVE CLI-NRO           TO PDC-NRO
024227     PERFORM UNIR-APE-NOM
024228     EVALUATE CLI-TIP-DOC
024229       WHEN '1'
024230     MOVE 'DNI'             TO PDC-TIPDOC
024231       WHEN '2'
024232     MOVE 'CUIT'            TO PDC-TIPDOC
024233       WHEN '3'
024234     MOVE 'CUIL'            TO PDC-TIPDOC
024235     END-EVALUATE
024236     MOVE CLI-NRO-DOC       TO PDC-NRODOC
024239     MOVE CLI-FEC-NACD      TO PDC-FECNAC-D
024240     MOVE CLI-FEC-NACM      TO PDC-FECNAC-M
024241     MOVE CLI-FEC-NACA      TO PDC-FECNAC-A
024243     MOVE CLI-CALLE         TO PDC-CALLE
024244     MOVE CLI-LOC-NUMERO    TO PDC-CALLE-NRO
024245     MOVE CLI-ZIP           TO PDC-CODPOST
024247     WRITE LINEA FROM PRINT-DET-CLI.
024248
024249 IMPRIMIR-REPO-MOV.
024250     IF WS-TIPO-REPO NOT EQUAL '1' THEN
024251        MOVE '1' TO WS-TIPO-REPO
024252        WRITE LINEA FROM WS-ESPACIO
024253        PERFORM IMPRIMIR-NOM-COLS
024254        ADD 3 TO WS-CANT-LIN
024260     END-IF
024300     MOVE INP-CLA-TIP       TO PDM-TIP
024400     MOVE INP-CLA-NRO       TO PDM-NRO
024500     PERFORM UNIR-APE-NOM
024600     MOVE CLI-SALDO         TO PDM-SAL-ACT
024700     MOVE WS-MONTO          TO PDM-IMPORTE
024800     MOVE WS-SALDO-ANTERIOR TO PDM-SAL-ANT
025000     WRITE LINEA FROM PRINT-DET-MOV.
025100
025200 IMPRIMIR-NO-VALIDO.
025210     MOVE INP-CLAVE TO CLI-CLAVE
025220     READ CLIENTES
025230     IF FS-CLI NOT = '00' THEN
025250         MOVE SPACES             TO PDI-APE-NOM
025270     ELSE
025500         PERFORM UNIR-APE-NOM
025510         IF WS-TIPO-REPO = '1' THEN
025520             MOVE PDM-APE-NOM    TO PDI-APE-NOM
025521             MOVE PDM-MOVI       TO PDI-MOVI
025530         ELSE
025540             MOVE PDC-APE-NOM    TO PDI-APE-NOM
025541             MOVE PDC-MOVI       TO PDI-MOVI
025550         END-IF
026110     END-IF
026120     MOVE INP-CLA-TIP        TO PDI-TIP
026130     MOVE INP-CLA-NRO        TO PDI-NRO
026140     WRITE LINEA FROM PRINT-DET-INV.
026200
026300 UNIR-APE-NOM.
026400     MOVE   25           TO WS-APE-LONG.
026500     PERFORM 25 TIMES
026600         IF CLI-APELLIDO(WS-APE-LONG:1) NOT EQUAL SPACES THEN
026700           CONTINUE
026800         ELSE
026900           SUBTRACT 1 FROM WS-APE-LONG GIVING WS-APE-LONG
027000         END-IF
027100     END-PERFORM
027101     STRING
027102         CLI-APELLIDO(1:WS-APE-LONG)
027103         ', '        DELIMITED BY SIZE
027104         CLI-NOMBRE  DELIMITED BY SIZE
027105         INTO WS-APE-NOM
027106     END-STRING
027110     IF WS-TIPO-REPO = '1' THEN
027120         MOVE WS-APE-NOM TO PDM-APE-NOM
027602     ELSE
027650         MOVE WS-APE-NOM TO PDC-APE-NOM
027660     END-IF.
