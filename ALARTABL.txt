000100 CBL LIB
000200 IDENTIFICATION DIVISION.
000300*****************************************************
000400*    REPORTE DE APELLIDOS CANTIDAD DE APELLIDOS     *
000500*****************************************************
000600 PROGRAM-ID.    ALARTABL.
000700 ENVIRONMENT DIVISION.
000800 CONFIGURATION SECTION.
000900 SPECIAL-NAMES.
001000      DECIMAL-POINT IS COMMA.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300     SELECT ENTRADA ASSIGN TO INPCLI
001400     FILE STATUS IS WS-ESTADO-ENTRADA.
001500
001600     SELECT SALIDA ASSIGN TO PRINT
001700     FILE STATUS IS WS-ESTADO-SALIDA.
001800
002200 DATA DIVISION.
002300 FILE SECTION.
002400 FD  ENTRADA
002500     RECORDING MODE F.
002600 01  REGISTRO-CLIENTE.
002700     05 REG-CLI-DNI      PIC 9(8).
002800     05 CLI-APELLIDO     PIC X(15).
002900     05 REG-CLI-NOM      PIC X(15).
003000     05 REG-CLI-CALLE    PIC X(20).
003100     05 REG-CLI-NRO      PIC 9(5).
003200     05 REG-CLI-LOCA     PIC X(20).
003300
003400     05 FILLER           PIC X(117).
003500 FD  SALIDA
003600     RECORDING MODE F.
003700 01  REG-SALIDA PIC X(132).
003800
004300 WORKING-STORAGE SECTION.
004310************** FILE STATUS Y FINALIZADOR ******
004400 01  WS-ESTADO-ENTRADA   PIC XX.
004500 01  WS-ESTADO-SALIDA    PIC XX.
004600 01  WS-FIN              PIC 9 VALUE 0.
004700     88 NOEOF            VALUE 0.
004800     88 EOFILE           VALUE 1.
004810
004900***************** TABLA ***********************
005000 01  TABLA-APELLIDO.
005100     05   TAB-ELEMENTO OCCURS 6000 TIMES.
005200          10   TAB-APE    PIC X(15) VALUE SPACES.
005300          10   TAB-CANT   PIC 9(5) VALUE ZERO.
005500
005600 01  IX                   PIC S9(8) BINARY.
005700 01  IX2                  PIC S9(8) BINARY.
005800
005900************* CONTADORES *********************
006000 01  CONT-LINE      PIC 9(2) VALUE 99.
006100 01  PAGINA         PIC 9(5) VALUE 0.
006200 01  CANT-APELLIDOS PIC 9(4) VALUE 0.
006300 01  CTL-LINEA      PIC 9 VALUE 1.
006400
006410************* TITULO PRINCIPAL ***************
006420 01 TITULO-PRIN.
006430     05       PIC X(29) VALUE SPACES.
006440     05       PIC X(18) VALUE 'TABLA DE APELLIDOS'.
006450     05       PIC X(85) VALUE SPACES.
006451 01 TITULO-SUBR.
006460     05       PIC X(29) VALUE SPACES.
006470     05  SUBR PIC X(18) VALUE ALL '-'.
006480     05       PIC X(85) VALUE SPACES.
006490
006500************* TITULO INICIAL ****************
006600 01  TITULO-INI.
006602     05              PIC X(1)  VALUE SPACES.
006610     05              PIC X(7)  VALUE 'Pagina:'.
006620     05 NUM-PAGINA   PIC Z.ZZ9.
006621     05              PIC X(1)  VALUE SPACES.
006630     05              PIC X(18) VALUE 'Creada en la fecha'.
006700     05              PIC X(1)  VALUE SPACES.
006900     05 FECHA        PIC 99/99/99.
006910     05              PIC X(1)  VALUE SPACES.
007000     05              PIC X(5)  VALUE 'a las'.
007010     05              PIC X(1)  VALUE SPACES.
007100     05 HORA         PIC 99.99.99.99.
007110     05              PIC X(4)  VALUE ' Hs.'.
007200     05              PIC X(62) VALUE SPACES.
007210
008200************* TITULO DETALLE *****************
008300 01  TITULO-DET.
008400     05       PIC X(27)  VALUE SPACES.
008500     05       PIC X(15)  VALUE 'Apellido'.
008600     05       PIC X(4)   VALUE SPACES.
008700     05       PIC X(08)  VALUE 'Cantidad'.
008800     05       PIC X(78)  VALUE SPACES.
008810
008900****************** DETALLE *******************
009000 01  DETALLE.
009100     05          PIC X(27)  VALUE SPACES.
009200     05 DET-APE  PIC X(15).
009300     05          PIC X(1)   VALUE SPACES.
009400     05 DET-CANT PIC ZZ.ZZ9.
009500     05          PIC X(83)  VALUE SPACES.
009600
009610****************** SEPARADORES ***************
009700 01  GUIONES     PIC X(132) VALUE ALL '-'.
009710 01  LINEA-BLK   PIC X(132) VALUE SPACES.
009800
009810****************** TOTALES *******************
009900 01  TOTALES.
010000     05               PIC X(5)  VALUE SPACES.
010100     05               PIC X(22) VALUE 'CANTIDAD DE APELLIDOS:'.
010200     05 TOT-APELLIDOS PIC Z.ZZ0.
010300     05               PIC X(100) VALUE SPACES.
010400
010800 PROCEDURE DIVISION.
010900 0000-INICIO.
010990
011000     INITIALIZE TABLA-APELLIDO.
011100     OPEN INPUT ENTRADA OUTPUT SALIDA.
011200     READ ENTRADA AT END  DISPLAY 'ARCHIVO VACIO' GOBACK.
011300
011310     WRITE REG-SALIDA FROM TITULO-PRIN.
011311     WRITE REG-SALIDA FROM TITULO-SUBR.
011312     WRITE REG-SALIDA FROM LINEA-BLK.
011320
011400     PERFORM UNTIL EOFILE
011500       PERFORM VARYING IX FROM 1 BY 1 UNTIL
011600             TAB-APE(IX) = CLI-APELLIDO OR
011700             TAB-CANT(IX) = 0  OR IX > 6000
011800       END-PERFORM
011900
012000       IF IX > 6000 THEN DISPLAY '*** TABLA LLENA'
012100           GOBACK END-IF
012200
012300       IF TAB-CANT(IX) = 0  THEN
012400         MOVE CLI-APELLIDO TO TAB-APE(IX)
012500       END-IF
012600       ADD 1 TO TAB-CANT(IX)
012700
012800
012900       READ ENTRADA AT END
013000            SET EOFILE TO TRUE
013100       END-READ
013200     END-PERFORM.
013300
013800     PERFORM CORTE.
013900     PERFORM VARYING IX2 FROM 1 BY 1 UNTIL IX2 > IX
014000         MOVE   TAB-APE(IX2)   TO DET-APE
014100         MOVE   TAB-CANT(IX2)  TO DET-CANT
014200         IF CONT-LINE > 90 THEN PERFORM CORTE
014300         END-IF
014400         WRITE REG-SALIDA FROM DETALLE
014500             AFTER ADVANCING CTL-LINEA LINES
014600         MOVE 1 TO CTL-LINEA
014700         ADD 1 TO CONT-LINE
014800         ADD 1 TO CANT-APELLIDOS
014900     END-PERFORM.
015000
015100     MOVE CANT-APELLIDOS TO TOT-APELLIDOS
015200     WRITE REG-SALIDA FROM GUIONES AFTER 2 LINES.
015300     WRITE REG-SALIDA FROM TOTALES
015400     WRITE REG-SALIDA FROM GUIONES AFTER 2 LINES.
015410
015500     CLOSE ENTRADA SALIDA.
015600     GOBACK.
015700
016100 CORTE.
016200     ACCEPT FECHA FROM DATE.
016300     ACCEPT HORA  FROM TIME.
016400     ADD 1 TO PAGINA.
016500     MOVE PAGINA TO NUM-PAGINA.
016600     WRITE REG-SALIDA FROM TITULO-INI AFTER ADVANCING PAGE.
016700     WRITE REG-SALIDA FROM GUIONES.
017000     WRITE REG-SALIDA FROM TITULO-DET.
017010     WRITE REG-SALIDA FROM LINEA-BLK.
017100     MOVE 2 TO CTL-LINEA.
017200     MOVE 1 TO CONT-LINE.
